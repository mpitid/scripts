#!/bin/zsh
### Convert between audio formats.
### Currently supports converting any format mplayer can handle, to
### mp3, ogg, flac or wav.

fail() {
    echo $@
    exit 1
}

require() {
    for CMD in $@; do
        which $CMD >/dev/null || fail "Required command not found: '$CMD'"
    done
}

overwrite() {
    file=$1
    if [[ -e $file ]]; then
        read answer\?"File '$file' exists. Overwrite (y/N)? "
        case ${answer:l} in
            y|yes) return 0;;
            *) return 1;;
        esac
    fi
    return 0
}

TMPDIR=`mktemp -t -d aconv.XXXXXX` || fail "Could not create temporary directory."
FIFO=$TMPDIR/sink

clean_exit() {
    rm -f $FIFO
    rmdir $TMPDIR
    exit 0
}

mkfifo $FIFO || fail "Could not create audio fifo."

trap 'clean_exit' SIGINT EXIT

require mplayer

[[ $# -lt 2 ]] && fail "Usage: $0 src dst [encoder options]"

SRC=$1
DST=$2
shift 2

TYPE=${DST:e}
if [[ -z $TYPE ]]; then
    TYPE=$DST
    DST=${SRC:r}.$DST
fi

overwrite $DST || fail "Choose a different target filename."

case ${TYPE:l} in
    mp3)
        require lame
        [ -z $@ ] && set -- -h --preset medium
        lame $FIFO -o $DST $@ &
        ;;
    ogg)
        require oggenc
        [ -z $@ ] && set -- -q 2
        oggenc $FIFO -o $DST $@ &
        ;;
    flac)
        require flac
        [ -z $@ ] && set -- -8 -f
        flac $FIFO -o $DST $@ &
        ;;
    wav)
        if which pv >/dev/null ; then
            # The size estimation is not accurate, but it's a good guess
            # for most mp3/ogg files.
            { pv -Wetpr -s $((12 * $(stat -c %s $SRC))) $FIFO > $DST } &
        else
            cat $FIFO > $DST &
        fi
        ;;
    *)
        fail "usage $0: source [target](mp3|ogg|flac|wav) [extra arguments]"
        ;;
esac

mplayer -nojoystick -quiet -benchmark -vc null -vo null -ao pcm:fast:file=${FIFO} $SRC
sleep 0.5s # Don't let encoder ouput mangle the shell prompt.

wait
tagcopy $SRC $DST
